#!/usr/bin/perl -w

=head1 NAME

dh_installpam - install pam support files

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installpam> [S<I<debhelper options>>] [B<--name=>I<name>]

=head1 DESCRIPTION

B<dh_installpam> is a debhelper program that is responsible for installing
files used by PAM into package build directories.

=head1 FILES

=over 4

=item debian/I<package>.pam

Installed into etc/pam.d/I<package> in the package build directory.

=back

=head1 OPTIONS

=over 4

=item B<--name=>I<name>

Look for files named debian/I<package>.I<name>.pam and install them as
etc/pam.d/I<name>, instead of using the usual files and installing them
using the package name.

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $pam=pkgfile($package,"pam");
	
	if ($pam ne '') {
		if (! -d "$tmp/etc/pam.d") {
			doit("install","-d","$tmp/etc/pam.d");
		}
		doit("install","-p","-m644",$pam,"$tmp/etc/pam.d/".pkgfilename($package));
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
