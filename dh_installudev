#!/usr/bin/perl -w

=head1 NAME

dh_installudev - install udev rules files

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use File::Find;

=head1 SYNOPSIS

B<dh_installudev> [S<I<debhelper options>>] [B<-n>] [B<--name=>I<name>] [B<--priority=>I<priority>]

=head1 DESCRIPTION

dh_installudev is a debhelper program that is responsible for
installing udev rules files.

Files named debian/package.udev will be installed in
etc/udev/rules.d/ in the package build directory.

Code is added to the preinst and postinst to handle the upgrade from the
old udev rules file location.

=head1 OPTIONS

=over 4

=item B<--name=>I<name>

When this parameter is used, dh_installudev looks for and
installs files named debian/package.name.udev instead of the usual
debian/package.udev.

=item B<--priority=>I<priority>

Sets the priority string of the rules.d symlink. Default is z60.

=item B<-n>, B<--noscripts>

Do not modify postinst/postrm scripts.

=back

=head1 NOTES

Note that this command is not idempotent. L<dh_prep(1)> should be called
between invocations of this command. Otherwise, it may cause multiple
instances of the same text to be added to maintainer scripts.

=cut

init();

if (! defined $dh{PRIORITY}) {
	$dh{PRIORITY}="z60";
}
if ($dh{PRIORITY}) {
	$dh{PRIORITY}.="_";
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $rules_file=pkgfile($package,"udev");
	my $filename=basename($rules_file);
	if ($filename eq 'udev') {
		$filename = "$package.udev";
	}
	$filename=~s/\.udev$/.rules/;

	if ($rules_file) {
		if (! -e "$tmp/etc/udev/rules.d") {
			doit("install","-d","$tmp/etc/udev/rules.d");
		}
		doit("install","-m","0644",$rules_file,"$tmp/etc/udev/rules.d/$dh{PRIORITY}$filename");
		if (! $dh{NOSCRIPTS}) {
			my $old="/etc/udev/$filename";
			my $rule="/etc/udev/rules.d/$dh{PRIORITY}$filename";
			autoscript($package,"preinst","preinst-udev","s!#OLD#!$old!g;s!#RULE#!$rule!g;s!#PACKAGE#!$package!g");
			autoscript($package,"postinst","postinst-udev","s!#OLD#!$old!g;s!#RULE#!$rule!g");
		}
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
