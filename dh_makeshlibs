#!/usr/bin/perl

=head1 NAME

dh_makeshlibs - automatically create shlibs file and call dpkg-gensymbols

=cut

use strict;
use warnings;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_makeshlibs> [S<I<debhelper options>>] [B<-m>I<major>] [B<-V>I<[dependencies]>] [B<-n>] [B<-X>I<item>] [S<B<--> I<params>>]

=head1 DESCRIPTION

B<dh_makeshlibs> is a debhelper program that automatically scans for shared
libraries, and generates a shlibs file for the libraries it finds.

It will also ensure that ldconfig is invoked during install and removal when
it finds shared libraries.  Since debhelper 9.20151004, this is done via a
dpkg trigger.  In older versions of debhelper, B<dh_makeshlibs> would
generate a maintainer script for this purpose.

=head1 FILES

=over 4

=item debian/I<package>.shlibs

Installs this file, if present, into the package as DEBIAN/shlibs.  If
omitted, debhelper will generate a shlibs file automatically if it
detects any libraries.

Note in compat levels 9 and earlier, this file was installed by
L<dh_installdeb(1)> rather than B<dh_makeshlibs>.

=item debian/I<package>.symbols

=item debian/I<package>.symbols.I<arch>

These symbols files, if present, are passed to L<dpkg-gensymbols(1)> to
be processed and installed. Use the I<arch> specific names if you need
to provide different symbols files for different architectures.

=back

=head1 OPTIONS

=over 4

=item B<-m>I<major>, B<--major=>I<major>

Instead of trying to guess the major number of the library with objdump,
use the major number specified after the -m parameter. This is much less
useful than it used to be, back in the bad old days when this program
looked at library filenames rather than using objdump.

=item B<-V>, B<-V>I<dependencies>

=item B<--version-info>, B<--version-info=>I<dependencies>

By default, the shlibs file generated by this program does not make packages
depend on any particular version of the package containing the shared
library. It may be necessary for you to add some version dependency
information to the shlibs file. If B<-V> is specified with no dependency
information, the current upstream version of the package is plugged into a
dependency that looks like "I<packagename> B<(E<gt>>= I<packageversion>B<)>". Note that in
debhelper compatibility levels before v4, the Debian part of the package
version number is also included. If B<-V> is specified with parameters, the
parameters can be used to specify the exact dependency information needed
(be sure to include the package name).

Beware of using B<-V> without any parameters; this is a conservative setting
that always ensures that other packages' shared library dependencies are at
least as tight as they need to be (unless your library is prone to changing
ABI without updating the upstream version number), so that if the
maintainer screws up then they won't break. The flip side is that packages
might end up with dependencies that are too tight and so find it harder to
be upgraded.

=item B<-n>, B<--no-scripts>

Do not add the "ldconfig" trigger even if it seems like the package
might need it.  The option is called B<--no-scripts> for historical
reasons as B<dh_makeshlibs> would previously generate maintainer
scripts that called B<ldconfig>.

=item B<-X>I<item>, B<--exclude=>I<item>

Exclude files that contain I<item> anywhere in their filename or directory 
from being treated as shared libraries.

=item B<--add-udeb=>I<udeb>

Create an additional line for udebs in the shlibs file and use I<udeb> as the
package name for udebs to depend on instead of the regular library package.

=item B<--> I<params>

Pass I<params> to L<dpkg-gensymbols(1)>.

=back

=head1 EXAMPLES

=over 4

=item B<dh_makeshlibs>

Assuming this is a package named F<libfoobar1>, generates a shlibs file that
looks something like:
 libfoobar 1 libfoobar1

=item B<dh_makeshlibs -V>

Assuming the current version of the package is 1.1-3, generates a shlibs
file that looks something like:
 libfoobar 1 libfoobar1 (>= 1.1)

=item B<dh_makeshlibs -V 'libfoobar1 (E<gt>= 1.0)'>

Generates a shlibs file that looks something like:
  libfoobar 1 libfoobar1 (>= 1.0)

=back

=cut

init(options => {
	"m=s", => \$dh{M_PARAMS},
	"major=s" => \$dh{M_PARAMS},
	"version-info:s" => \$dh{V_FLAG},
	"add-udeb=s" => \$dh{SHLIBS_UDEB},
});

my $objdump=cross_command("objdump");

my $ok=1;

foreach my $package (@{$dh{DOPACKAGES}}) {
	next if is_udeb($package);
	
	my $tmp=tmpdir($package);

	my (%seen, $unversioned_so);
	my $need_ldconfig = 0;
	my $shlibs_file = pkgfile($package, 'shlibs');

	doit("rm", "-f", "$tmp/DEBIAN/shlibs");

	# So, we look for files or links to existing files with names that
	# match "*.so.*". And we only look at real files not
	# symlinks, so we don't accidentally add shlibs data to -dev
	# packages. This may have a few false positives, which is ok,
	# because only if we can get a library name and a major number from
	# objdump is anything actually added.
	my $exclude='';
	my (@udeb_lines, @lib_files);
	if (defined($dh{EXCLUDE_FIND}) && $dh{EXCLUDE_FIND} ne '') {
		$exclude="! \\( $dh{EXCLUDE_FIND} \\) ";
	}
	open (FIND, "find $tmp -type f \\( -name '*.so' -or -name '*.so.*' \\) $exclude | LC_ALL=C sort |");
	while (<FIND>) {
		my ($library, $major);
		push @lib_files, $_;
		my $ret=`$objdump -p $_`;
		if ($ret=~m/\s+SONAME\s+(.*)\.so\.(.*)/) {
			# proper soname format
			$library=$1;
			$major=$2;
		}
		elsif ($ret=~m/\s+SONAME\s+(.*)-(\d.*)\.so/) {
			# idiotic crap soname format
			$library=$1;
			$major=$2;
		} elsif ($ret =~ m/\s+SONAME\s+(?:.*)\.so/) {
			$unversioned_so = 1;
		}

		if (defined($dh{M_PARAMS}) && $dh{M_PARAMS} ne '') {
			$major=$dh{M_PARAMS};
		}
		
		install_dir("$tmp/DEBIAN");
		my $deps=$package;
		if ($dh{V_FLAG_SET}) {
			if ($shlibs_file) {
				warning("The provided ${shlibs_file} file overwrites -V");
				# Clear the flag to avoid duplicate warnings.  Note
				# we can safely fallthrough as the result will be
				# replaced regardless.
				$dh{V_FLAG_SET} = 0;
			}
			if ($dh{V_FLAG} ne '') {
				$deps=$dh{V_FLAG};
			}	
			else {
				# Call isnative because it sets $dh{VERSION}
				# as a side effect.
				isnative($package);
				my $version = $dh{VERSION};
				# Old compatibility levels include the
				# debian revision, while new do not.
				# Remove debian version, if any.
				$version =~ s/-[^-]+$//;
				$deps="$package (>= $version)";
			}
		}
		if (defined($library) && defined($major) && defined($deps) &&
		      $library ne '' && $major ne '' && $deps ne '') {
			$need_ldconfig=1;
			# Prevent duplicate lines from entering the file.
			my $line="$library $major $deps";
			if (! $seen{$line}) {
				$seen{$line}=1;
				complex_doit("echo '$line' >>$tmp/DEBIAN/shlibs");
				if (defined($dh{SHLIBS_UDEB}) && $dh{SHLIBS_UDEB} ne '') {
					my $udeb_deps = $deps;
					$udeb_deps =~ s/\Q$package\E/$dh{SHLIBS_UDEB}/e;
			    		$line="udeb: "."$library $major $udeb_deps";
					push @udeb_lines, $line;
				}
			}
		}
	}
	close FIND;

	# Write udeb: lines last.
	foreach (@udeb_lines) {
		complex_doit("echo '$_' >>$tmp/DEBIAN/shlibs");
	}

	if ($shlibs_file) {
		install_dir("$tmp/DEBIAN");
		install_file($shlibs_file, "$tmp/DEBIAN/shlibs");
	}

	if (-e "$tmp/DEBIAN/shlibs") {
		reset_perm_and_owner('0644', "$tmp/DEBIAN/shlibs");
	}

	# dpkg-gensymbols files
	my $symbols=pkgfile($package, "symbols");
	if (-e $symbols) {
		my @liblist;
		if (! compat(7)) {
			@liblist=map { "-e$_" } @lib_files;
		}
		# -I is used rather than using dpkg-gensymbols
		# own search for symbols files, since that search
		# is not 100% compatible with debhelper. (For example,
		# this supports --ignore being used.)
		$ok = doit_noerror(
				"dpkg-gensymbols",
				"-p$package",
				"-I$symbols",
				"-P$tmp",
				@liblist,
				@{$dh{U_PARAMS}}
			) && $ok;

		if (-f "$tmp/DEBIAN/symbols" and -s _ == 0) {
			doit("rm", "-f", "$tmp/DEBIAN/symbols");
		} elsif ($unversioned_so) {
			# There are a few "special" libraries (e.g. nss/nspr)
			# which do not have versined SONAMES.  However the
			# maintainer provides a symbols file for them and we can
			# then use that to add an ldconfig trigger.
			$need_ldconfig = 1;
		}
	}

	# Historically, --no-scripts would disable the creation of
	# maintscripts for calling ldconfig.
	if (! $dh{NOSCRIPTS} && $need_ldconfig) {
		autotrigger($package, 'activate-noawait', 'ldconfig');
	}
}

unless ($ok) {
	error "failing due to earlier errors";
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut

# Local Variables:
# indent-tabs-mode: t
# tab-width: 4
# cperl-indent-level: 4
# End:
