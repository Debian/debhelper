#!/usr/bin/perl -w

=head1 NAME

dh_python - calculates Python dependencies and adds postinst and prerm Python scripts (deprecated)

=cut

use strict;
use File::Find;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_python> [S<I<debhelper options>>] [B<-n>] [B<-V> I<version>] [S<I<module dirs> ...>]

=head1 DESCRIPTION

Note: This program is deprecated. You should use B<dh_python2> instead.
This program will do nothing if F<debian/pycompat> or a
B<Python-Version> F<control> file field exists.

B<dh_python> is a debhelper program that is responsible for generating the
B<${python:Depends}> substitutions and adding them to substvars files. It
will also add a F<postinst> and a F<prerm> script if required.

The program will look at Python scripts and modules in your package, and
will use this information to generate a dependency on B<python>, with the
current major version, or on B<python>I<X>B<.>I<Y> if your scripts or modules need a
specific B<python> version. The dependency will be substituted into your
package's F<control> file wherever you place the token B<${python:Depends}>.

If some modules need to be byte-compiled at install time, appropriate
F<postinst> and F<prerm> scripts will be generated. If already byte-compiled
modules are found, they are removed.

If you use this program, your package should build-depend on B<python>.

=head1 OPTIONS

=over 4

=item I<module dirs>

If your package installs Python modules in non-standard directories, you
can make F<dh_python> check those directories by passing their names on the
command line. By default, it will check F</usr/lib/site-python>,
F</usr/lib/$PACKAGE>, F</usr/share/$PACKAGE>, F</usr/lib/games/$PACKAGE>,
F</usr/share/games/$PACKAGE> and F</usr/lib/python?.?/site-packages>.

Note: only F</usr/lib/site-python>, F</usr/lib/python?.?/site-packages> and the
extra names on the command line are searched for binary (F<.so>) modules.

=item B<-V> I<version>

If the F<.py> files your package ships are meant to be used by a specific
B<python>I<X>B<.>I<Y> version, you can use this option to specify the desired version,
such as B<2.3>. Do not use if you ship modules in F</usr/lib/site-python>.

=item B<-n>, B<--noscripts>

Do not modify F<postinst>/F<prerm> scripts.

=back

=head1 CONFORMS TO

Debian policy, version 3.5.7

Python policy, version 0.3.7

=cut

init();

if (-e "debian/pycompat") {
	warning("Doing nothing since dh_pycompat exists; dh_pysupport or dh_pycentral should do the work. You can remove dh_python from your rules file.");
	exit 0;
}
elsif (`grep Python-Version: debian/control`) {
	warning("Doing nothing since Python-Version is set; dh_python2 should do the work. You can remove dh_python from your rules file.");
	exit 0;
}
else {
	warning("This program is deprecated, you should use dh_python2 instead.");
}

my $python = 'python';

# The current python major version
my $python_major;
my $python_version = `$python -V 2>&1`;
if (! defined $python_version || $python_version eq "") {
	error("Python is not installed, aborting. (Probably forgot to Build-Depend on python.)");
}
elsif ($python_version =~ m/^Python\s+(\d+)\.(\d+)(\.\d+)*/) {
	$python_version = "$1.$2" ;
	$python_major = $1 ;
} else { 
	error("Unable to parse python version out of \"$python_version\".");
}

# The next python version
my $python_nextversion = $python_version + 0.1;
my $python_nextmajor = $python_major + 1;

my @python_allversions = ('1.5','2.1','2.2','2.3','2.4');
foreach (@python_allversions) {
	s/^/python/;
}

# Check for -V
my $usepython = "python$python_version";
if($dh{V_FLAG_SET}) {
	$usepython = $dh{V_FLAG};
	$usepython =~ s/^/python/;
	if (! grep { $_ eq $usepython } @python_allversions) {
		error("Unknown python version $dh{V_FLAG}");
	}
}

# Cleaning the paths given on the command line
foreach (@ARGV) {
	s#/$##;
	s#^/##;
}

# dependency types
use constant PROGRAM   => 1;
use constant PY_MODULE => 2;
use constant PY_MODULE_NONSTANDARD => 4;
use constant SO_MODULE => 8;
use constant SO_MODULE_NONSTANDARD => 16;

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp = tmpdir($package);

	my @dirs = ("usr/lib/site-python", "usr/lib/$package", "usr/share/$package", "usr/lib/games/$package", "usr/share/games/$package", @ARGV );
	my @dirs_so = ("usr/lib/site-python", @ARGV );

	my $dep_on_python = 0;
	my $strong_dep = 0;
	my $look_for_pythonXY = 1;

	# First, the case of python-foo and pythonX.Y-foo
	if ($package =~ /^python-/) {
		$dep_on_python = 1;
		$strong_dep = 1;
		my $pack = $package;
		$pack =~ s/^python/python$python_version/;
		if (grep { "$_" eq "$pack" } getpackages()) {
			addsubstvar($package, "python:Depends", $pack);
		}
	}
	if ($package !~ /^python[0-9].[0-9]-/) {
		push @dirs, "usr/lib/$usepython/site-packages";
		push @dirs_so, "usr/lib/$usepython/site-packages";
		$look_for_pythonXY = 0;
	}

	@dirs = grep -d, map "$tmp/$_", @dirs;
	@dirs_so = grep -d, map "$tmp/$_", @dirs_so;

	my $deps = 0;
	my %verdeps = ();
	foreach (@python_allversions) {
		$verdeps{$_} = 0;
	}

	# Find scripts
	find sub {
		return unless -f and (-x or /\.py$/);
		local *F;
		return unless open F, $_;
		if (read F, local $_, 32 and m%^#!\s*/usr/bin/(env\s+)?(python(\d+\.\d+)?)\s%) {
			if ( "python" eq $2 ) {
				$deps |= PROGRAM;
			} elsif(defined $verdeps{$2}) {
				$verdeps{$2} |= PROGRAM;
			}
		}
		close F;
	}, $tmp;

	# Look for python modules
	my $dirlist="";
	if (@dirs) {
		foreach my $curdir (@dirs) {
			my $has_module = 0;
			$curdir =~ s%^$tmp/%%;
			find sub {
				return unless -f;
				if (/\.py$/) {
					$has_module = 1;
					doit(("rm","-f",$_."c",$_."o"));
				}
			}, "$tmp/$curdir" ;
			if ($has_module) {
				if ($dh{V_FLAG_SET}) {
					$verdeps{$usepython} |= PY_MODULE_NONSTANDARD;
				} else {
					$deps |= PY_MODULE;
				}
				$dirlist="$dirlist /$curdir";
			}
		}
	}
	if (@dirs_so) {
		foreach my $curdir (@dirs_so) {
			my $has_module = 0;
			$curdir =~ s%^$tmp/%%;
			find sub {
				return unless -f;
				$has_module = 1 if /\.so$/;
			}, "$tmp/$curdir" ;
			if ($has_module) {
				if ($dh{V_FLAG_SET}) {
					$verdeps{$usepython} |= SO_MODULE_NONSTANDARD;
				}
				else {
					$deps |= SO_MODULE;
				}
			}
		}
	}

	# Dependencies on current python
	$dep_on_python = 1 if $deps;
	$strong_dep = 1 if($deps & (PY_MODULE|SO_MODULE));

	if ($dep_on_python) {
		addsubstvar($package, "python:Depends", $python, ">= $python_version");
		if ($strong_dep) {
			addsubstvar($package, "python:Depends", $python, "<< $python_nextversion");
		} else {
			addsubstvar($package, "python:Depends", $python, "<< $python_nextmajor");
		}
	}

	my $need_prerm = 0;

	# Look for specific pythonX.Y modules
	foreach my $pyver (@python_allversions) {
		my $pydir="/usr/lib/$pyver/site-packages";
		if ($look_for_pythonXY) {
			if (grep -d,"$tmp$pydir") {
				find sub {
					return unless -f;
					if (/\.py$/) {
						$verdeps{$pyver} |= PY_MODULE;
						doit(("rm","-f",$_."c",$_."o"));
					}
					$verdeps{$pyver} |= SO_MODULE if /\.so$/;
				}, "$tmp$pydir";
			}
		}
	
		# Go for the dependencies
		addsubstvar($package, "python:Depends", $pyver) if $verdeps{$pyver};

		# And now, the postinst and prerm stuff
		if ($pyver eq "$usepython") {
			if ($verdeps{$pyver} & PY_MODULE) {
				$pydir = $pydir.$dirlist;
			} else {
				$pydir = $dirlist;
			}
			$verdeps{$pyver} |= PY_MODULE if($deps & PY_MODULE);
		}
		if ($verdeps{$pyver} & (PY_MODULE|PY_MODULE_NONSTANDARD) && ! $dh{NOSCRIPTS}) {
			autoscript($package,"postinst","postinst-python","s%#PYVER#%$pyver%;s%#DIRLIST#%$pydir%");
			$need_prerm = 1;
		}
	}
	if ($need_prerm && ! $dh{NOSCRIPTS}) {
		autoscript($package,"prerm","prerm-python","s%#PACKAGE#%$package%");
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Josselin Mouette <joss@debian.org>

most ideas stolen from Brendan O'Dea <bod@debian.org>

=cut
